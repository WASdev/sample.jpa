apply plugin: 'war'
apply plugin: 'liberty'

group = 'liberty.gradle'
version = '1'

description = "ava Persistence API (JPA) Sample"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:1.1-SNAPSHOT'
    }
}

repositories {
     mavenCentral()
}

ext {
    // Liberty server properties
    wlpServerName = 'LibertyProjectServer'
    serverDirectory = "${project.buildDir}/wlp/usr/servers/${wlpServerName}"
    testServerHttpPort = 9080
    testServerHttpsPort = 9443

    warContext = 'jpaApp'

}

war {
    archiveName = baseName + '.' + extension
}

liberty {
    server{
        configFile = file("src/main/liberty/config/server.xml")
		bootstrapProperties = ['default.http.port': "${testServerHttpPort}" , 'default.https.port': "${testServerHttpsPort}", 'appContext': "${warContext}",'appLocation': "${war.archiveName}"]
        name = 'LibertyProjectServer'
        apps = [war]
		features {
            name = ['servlet-3.1', 'jdbc-4.1', 'jpa-2.0', 'localConnector-1.0']
            acceptLicense = true
        }
     }
}


configurations{
     derby { transitive = true }
}

dependencies {
    compile group: 'javax.persistence', name: 'persistence-api', version:'1.0.2'
    compile group: 'javax.transaction', name: 'javax.transaction-api', version:'1.2'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    derby group: 'org.apache.derby', name: 'derby', version: '10.13.1.1'
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-kernel', version: '17.0.0.2'
}


task copyDerby {
    doLast {
        copy {
            from configurations.derby
            into "$buildDir/wlp/usr/shared/resources"
            include '*.jar'
        }
    }
}

test {
    println 'inside the test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.name': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

check.dependsOn 'integrationTest'
copyDerby.dependsOn 'installApps'
libertyStart.dependsOn 'installFeature', 'copyDerby'
integrationTest.dependsOn 'libertyStart', 'testClasses'
integrationTest.finalizedBy 'libertyStop'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
